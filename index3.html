<!DOCTYPE html>
<html>

  <head>

  <!-- custom CSS -->
  </head>

  <body>
    <div id="container"></div>
  </body>

  <script type="x-shader/x-vertex" id="vertexshader">
    attribute vec3 in_Position;
    varying vec2 fragCoord;
    varying vec2 vUv; 
    void main()
    {
        vUv = uv;
        vec4 mvPosition = modelViewMatrix * vec4(position, 1.0 );
        gl_Position = projectionMatrix * mvPosition;
        fragCoord = position.xy;
    }
	</script>

	<script type="x-shader/x-fragment" id="fragmentshader">
	 uniform float iGlobalTime;
    uniform vec2 iResolution;
    uniform vec4      iMouse;
    uniform sampler2D iChannel0;
    varying vec2 fragCoord;
    varying vec2 vUv;
    
  float calc( vec2 p, float time )
{
	p = -1.0 + 2.0*p;
	p.x *= iResolution.x/iResolution.y;

	float ltime = 0.5-0.5*cos(time*0.12);
  float zoom = pow( 0.9, 100.0*ltime );
	float an = 2.0*ltime;
	p = mat2(cos(an),sin(an),-sin(an),cos(an))*p;
	vec2 ce = vec2( 0.2655,0.301 );
	ce += zoom*0.8*cos(4.0+4.0*ltime);
	p = ce + (p-ce)*zoom;
	vec2 c = vec2( -0.745, 0.186 ) - 0.045*zoom*(1.0-ltime);
	
	vec2 z = p;
   
#if 0
    // full derivatives version
	vec2 dz = vec2( 1.0, 0.0 );
	for( int i=0; i<256; i++ )
	{
		dz = 2.0*vec2(z.x*dz.x-z.y*dz.y, z.x*dz.y + z.y*dz.x );
        z = vec2( z.x*z.x - z.y*z.y, 2.0*z.x*z.y ) + c;
		if( dot(z,z)>200.0 ) break;
	}
	float d = sqrt( dot(z,z)/dot(dz,dz) )*log(dot(z,z));

#else
    // only derivative length version
    float ld2 = 1.0;
    float lz2 = dot(z,z);
    for( int i=0; i<256; i++ )
	{
        z = vec2( z.x*z.x - z.y*z.y, 2.0*z.x*z.y ) + c;
        ld2 *= 4.0*lz2;
        lz2 = dot(z,z);
		if( lz2>200.0 ) break;
	}
    float d = sqrt(lz2/ld2)*log(lz2);

#endif
    
	return pow( clamp( (150.0/zoom)*d, 0.0, 1.0 ), 0.5 );
}

	
void main()
{
	#if 0
	float scol = calc( fragCoord.xy/iResolution.xy, iGlobalTime );
    #else

    float scol = 0.0;
	for( int j=0; j<2; j++ )
	for( int i=0; i<2; i++ )
	{
		vec2 of = -0.5 + vec2( float(i), float(j) ) / 2.0;
	    scol += calc( (fragCoord.xy+of)/iResolution.xy, iGlobalTime );
	}
	scol *= 0.25;

    #endif
	
	vec3 vcol = pow( vec3(scol), vec3(0.9,1.1,1.4) );
	
	vec2 uv = fragCoord.xy/iResolution.xy;
	vcol *= 0.7 + 0.3*pow(16.0*uv.x*uv.y*(1.0-uv.x)*(1.0-uv.y),0.25);

	
	gl_FragColor = vec4( vcol, 1.0 );
}
	</script>

  <!-- jquery js -->
  <script src="https://ajax.googleapis.com/ajax/libs/jquery/1.5.1/jquery.min.js"></script>

  <!-- three js -->
  <script src='//cdnjs.cloudflare.com/ajax/libs/three.js/r75/three.js'></script> 

  <!-- custom js -->
  <script src="script.js"></script>

</html>
